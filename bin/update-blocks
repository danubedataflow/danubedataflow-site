#!/usr/bin/env perl
use strict;
use warnings;
use Path::Tiny;
use utf8;
my %replace;

# The 'header' block
$replace{header} = <<~BLOCK;
    <header>
        <h1 class="site-title"><a href="/">Danube Dataflow</a></h1>
        <h2 class="site-byline">Minimal digital algorithmic art. Marcel Grünauer.</h2>
        <div class="nav">
            <a href="/">Works</a>
            | <a href="/principles/">Principles</a>
            | <a href="/notes/">Notes</a>
            | <a href="https://github.com/danubedataflow/danubedataflow-site" target="_blank">GitHub</a>
        </div>
    </header>
    BLOCK

# The 'metadata' block
$replace{metadata} = <<~BLOCK;
    <meta charset="utf-8">
    <meta name="keywords" content="Algorithmic art, digital art, generative design, creative programming, creative coding">
    <meta name="description" content="Minimal algorithmic art from the Danube metropolis.">
    <meta name="author" content="Marcel Grünauer">
    <meta name="viewport" content="width=device-width, initial-scale=1, maximum-scale=1, user-scalable=0">
    <meta name="mobile-web-app-capable" content="yes">
    <link rel="icon" href="/favicon.ico" type="image/x-icon">
    <link rel="apple-touch-icon" href="/apple-touch-icon.png">
    <link href="/css/style.css" rel="stylesheet">
    BLOCK

# The 'work' block
$replace{work} = <<~BLOCK;
    <main>
        <section id="work-container">
            <div id="work"><canvas></canvas></div>

            <div id="controls">
                <p>
                    <span id="workTitle"></span>
                    <a id="goToNewerWork" href="#"><span title="Go to newer work (left arrow)">&#x2B05;&#xFE0F;</span></a>
                    <a id="goToOlderWork" href="#"><span title="Go to older work (right arrow)">&#x27A1;&#xFE0F;</span></a>
                    |
                    <span id="createdDate"></span> |
                    <span id="canvasSize"></span> |
                    <a id="sourceLink" href="#" target="_blank">Source code</a> |
                    <a id="saveCanvas" href="#">Save</a> |
                    <a id="copyLink" href="#">Copy link</a>
                </p>

                <button id="redrawWithNewSeed" class="button">New random seed</button>
                <button id="setControlsRandomly" class="button">Random parameters</button>

                <details id="description">
                    <summary></summary>
                </details>

                <form id="controls-form">
                </form>

            </div>
        </section>
    </main>
    BLOCK
my $iter = path('src')->iterator({ recurse => 1 });
while (my $next = $iter->()) {
    next unless $next =~ /.html$/;
    my @html   = $next->lines_utf8;
    my @output = replace_blocks(\@html, \%replace);
    $next->spew_utf8(@output);
}

sub replace_blocks {
    my ($lines_array, $replacement_hash) = @_;
    my $current;
    my @output;
    for ($lines_array->@*) {

        # detect BEGIN marker
        if (/<!--\s*BEGIN\s+(\w+)\s*-->/) {
            $current = $1;
            push @output, $_;
            next;
        }

        # detect END marker
        if (/<!--\s*END\s+(\w+)\s*-->/) {
            push @output, $replace{$current};
            undef $current;
            push @output, $_;
            next;
        }

        # inside a block: replace content
        if ($current && exists $replace{$current}) {
            next;
        }

        # outside any block
        push @output, $_;
    }
    return @output;
}

=pod

Iterates over all HTML files in src/. In each files, it tries to find delimited
blocks like this:

    <!-- BEGIN header -->
    ...
    <!-- END header -->

and replaces their contents with the lines in the corresponding entry in the
%replace hash.

So this acts as a mini-templating engine. If you want to change anything within
one of the reused blocks, change it here, then rerun this program. Then `make
tidy`.

=cut

#!/usr/bin/env perl
use strict;
use warnings;
use feature qw(:5.36);
use FindBin qw($Bin);
use lib "$Bin/../perllib";
use Getopt::Long;
use DanubeDataflow;
use Path::Tiny;
use open qw(:std :utf8);
my %opt;
GetOptions(\%opt, qw(src|s=s www|w=s)) or die;
for (qw(src www)) { defined $opt{$_} or die "need --$_\n" }
my $combined = i18n_assemble_dicts($opt{src});
my $iter     = path($opt{www})->iterator({ recurse => 1 });

while (my $next = $iter->()) {
    next unless $next =~ /\.html$/;
    my $html = $next->slurp_utf8;
    $html =~
      s!\bdata-i18n-key="(.*?)".*?>\K(?=<)! $combined->{en}{$1} // "missing key: $1" !ge;
    $next->spew_utf8($html);
}

=pod

This program crudely adds English texts as the inner texts of tags that use the
C<data-i18n-key> attribute.

This way the English texts are already on the live site so Google can index
them.

=cut
